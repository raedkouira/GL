parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import java.io.PrintStream;
	import fr.ensimag.deca.tools.SymbolTable;
}

@members {
    @Override
    protected AbstractProgram parseProgram() {
        return prog().tree;
    }
    SymbolTable tableSymbol = new SymbolTable();
}

prog returns[AbstractProgram tree]
    : list_classes main EOF {
            assert($list_classes.tree != null);
            assert($main.tree != null);
            $tree = new Program($list_classes.tree, $main.tree);
            setLocation($tree, $list_classes.start);
            setLocation($main.tree, $main.start);
        }
    ;

main returns[AbstractMain tree]
    : /* epsilon */ {
            $tree = new EmptyMain();
        }
    | block {
            assert($block.decls != null);
            assert($block.insts != null);
            $tree = new Main($block.decls, $block.insts);
            setLocation($tree, $block.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            $insts = $list_inst.tree;
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
    	    assert($dv1.tree != null);
            $l.add($dv1.tree);
            setLocation($dv1.tree, $dv1.start);
        } (COMMA dv2=decl_var[$t] {
            assert($dv2.tree != null);
            $l.add($dv2.tree);
            setLocation($dv2.tree, $dv2.start);
        }
      )*
    ;

decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree]
@init   {
        Initialization init_var;
        }
    : i=ident {
    		assert($t != null);
    		assert($i.tree != null);
    		$tree = new DeclVar($t, $i.tree, new NoInitialization());
            setLocation($i.tree, $i.start);
        }
      (EQUALS e=expr {
      		assert($t != null);
      		assert($i.tree != null);
      		assert($e.tree != null);
            init_var = new Initialization($e.tree);
            setLocation(init_var, $EQUALS);
			$tree = new DeclVar($t, $i.tree, init_var);
            setLocation($e.tree, $e.start);
        }
      )? {
        }
    ;

list_inst returns[ListInst tree]
@init {
$tree = new ListInst();
}
    : (e1 = inst {

    	    assert($e1.tree != null);
    	    $tree.add($e1.tree);
            // La localisation de l'instruction matchée est celle du premier jeton qu'elle comporte
            setLocation($e1.tree, $e1.start);

        }
      )*
    ;

inst returns[AbstractInst tree]
    : e1=expr SEMI {
            assert($e1.tree != null);
            $tree = $e1.tree;
            setLocation($tree, $e1.start);
        }
    | SEMI {
            $tree = new NoOperation();
        }
    | PRINT OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(false, $list_expr.tree);
        }
    | PRINTLN OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);

			$tree = new Println(false, $list_expr.tree);
        }
    | PRINTX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
			$tree = new Print(true, $list_expr.tree);
        }
    | PRINTLNX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
			$tree = new Println(true, $list_expr.tree);
        }
    | if_then_else {
            assert($if_then_else.tree != null);
            $tree = $if_then_else.tree;
        }
    | WHILE OPARENT condition=expr CPARENT OBRACE body=list_inst CBRACE {
            assert($condition.tree != null);
            assert($body.tree != null);
            $tree = new While($condition.tree, $body.tree);
        }
    | RETURN expr SEMI {
            assert($expr.tree != null);
            $tree = new Return($expr.tree);
            setLocation($expr.tree, $expr.start);
            setLocation($tree, $RETURN);
        }
    ;

if_then_else returns[IfThenElse tree]
@init {
    IfThenElse temp;
    ListInst list_inst_else = new ListInst();
    ListInst list_inst_else_tmp;
    ListInst list_inst_else_if = new ListInst();
}
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
           $tree = new IfThenElse($condition.tree, $li_if.tree, list_inst_else);
           list_inst_else_tmp = list_inst_else;
        }
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {
           list_inst_else_if = new ListInst();
           temp = new IfThenElse($elsif_cond.tree, $elsif_li.tree, list_inst_else_if);
           setLocation(temp, $ELSE);
           list_inst_else_tmp.add(temp);
           list_inst_else_tmp = list_inst_else_if;
        }
      )*
      (ELSE OBRACE li_else=list_inst CBRACE {
           temp = new IfThenElse($condition.tree, new ListInst(), $li_else.tree);
           setLocation(temp, $ELSE);
           list_inst_else_tmp.add(temp);
        }
      )?
    ;

list_expr returns[ListExpr tree]
@init   {
            $tree = new ListExpr();
        }
    : (e1=expr {
		assert($e1.tree != null);
		$tree.add($e1.tree);
        }
       (COMMA e2=expr {
		assert($e2.tree != null);
		$tree.add($e2.tree);
        }
       )* )?
    ;

expr returns[AbstractExpr tree]
    : assign_expr {

        assert($assign_expr.tree != null);
	    $tree = $assign_expr.tree;
        setLocation($tree, $assign_expr.start);
        }
    ;

assign_expr returns[AbstractExpr tree]
    : e=or_expr (
        /* condition: expression e must be a "LVALUE" */ {
            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            }
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
        EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);

            $tree = new Assign((AbstractLValue)$e.tree, $e2.tree);
            setLocation($tree, $EQUALS);
        }
      | /* epsilon */ {
            assert($e.tree != null);
	        $tree = $e.tree;
            setLocation($tree, $e.start);

        }
      )
    ;

or_expr returns[AbstractExpr tree]
    : e=and_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=or_expr OR e2=and_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Or($e1.tree, $e2.tree);
            setLocation($tree, $OR);
       }
    ;

and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    |  e1=and_expr AND e2=eq_neq_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new And($e1.tree, $e2.tree);
            setLocation($tree, $AND);
        }
    ;

eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=eq_neq_expr EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Equals($e1.tree, $e2.tree);
            setLocation($tree, $EQEQ);
        }
    | e1=eq_neq_expr NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new NotEquals($e1.tree, $e2.tree);
            setLocation($tree, $NEQ);
        }
    ;

inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=inequality_expr LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new LowerOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $LEQ);
        }
    | e1=inequality_expr GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new GreaterOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $GEQ);
        }
    | e1=inequality_expr GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Greater($e1.tree, $e2.tree);
            setLocation($tree, $GT);
        }
    | e1=inequality_expr LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Lower($e1.tree, $e2.tree);
            setLocation($tree, $LT);
        }
    | e1=inequality_expr INSTANCEOF type {
            assert($e1.tree != null);
            assert($type.tree != null);
            if (true)
                throw new UnsupportedOperationException("Non géré dans le cadre du langage sans objet");
        }
    ;


sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=sum_expr PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Plus($e1.tree,  $e2.tree);
            setLocation($tree, $PLUS);
        }
    | e1=sum_expr MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Minus($e1.tree,  $e2.tree);
            setLocation($tree, $MINUS);
        }
    ;

mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=mult_expr TIMES e2=unary_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Multiply($e1.tree, $e2.tree);
            // A FAIRE: Gérer le set location ici
            setLocation($tree, $TIMES);
        }
    | e1=mult_expr SLASH e2=unary_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Divide($e1.tree, $e2.tree);
            setLocation($tree, $SLASH);
        }
    | e1=mult_expr PERCENT e2=unary_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Modulo($e1.tree, $e2.tree);
            setLocation($tree, $PERCENT);
        }
    ;

unary_expr returns[AbstractExpr tree]
    : op=MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
            setLocation($tree, $op);
        }
    | op=EXCLAM e=unary_expr {
            assert($e.tree != null);
			$tree = new Not($e.tree);
            setLocation($tree, $op);
        }
    | select_expr {
            assert($select_expr.tree != null);
            $tree = $select_expr.tree;
            setLocation($tree, $select_expr.start);
        }
    ;

select_expr returns[AbstractExpr tree]
    : e=primary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=select_expr DOT i=ident {
            assert($e1.tree != null);
            assert($i.tree != null);
            $tree = new Selection($e1.tree, $i.tree);
            setLocation($tree, $DOT);
            setLocation($e1.tree, $e1.start);
            setLocation($i.tree, $i.start);
        }
        (o=OPARENT args=list_expr CPARENT {
            // we matched "e1.i(args)"
            assert($args.tree != null);
            $tree = new MethodCall($e1.tree, $i.tree, $args.tree);
            setLocation($args.tree, $args.start);
        }
        | /* epsilon */ {
            // we matched "e.i"
        }
        )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($tree, $ident.start);
        }
    | m=ident OPARENT args=list_expr CPARENT {
            assert($args.tree != null);
            assert($m.tree != null);
            AbstractExpr this_expr = new This();
            $tree = new MethodCall(this_expr, $m.tree, $args.tree);
            setLocation($tree, $OPARENT);
            setLocation(this_expr, $m.start);
            setLocation($m.tree, $m.start);
            setLocation($args.tree, $args.start);
        }
    | OPARENT expr CPARENT {
            assert($expr.tree != null);
            $tree = $expr.tree;
        }
    | READINT OPARENT CPARENT {
            $tree = new ReadInt();
        }
    | READFLOAT OPARENT CPARENT {
            $tree = new ReadFloat();
        }
    | NEW ident OPARENT CPARENT {
            assert($ident.tree != null);
            $tree = new New($ident.tree);
            setLocation($tree, $NEW);
            setLocation($ident.tree, $ident.start);
        }
    | cast=OPARENT type CPARENT OPARENT expr CPARENT {
            assert($type.tree != null);
            assert($expr.tree != null);
            if (true)
                throw new UnsupportedOperationException("Non géré dans le cadre du langage objet");
        }
    | literal {
            assert($literal.tree != null);
            $tree = $literal.tree;

            setLocation($tree, $literal.start);
        }
    ;

type returns[AbstractIdentifier tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($tree, $ident.start);
        }
    ;

literal returns[AbstractExpr tree]
    : in=INT {
            try {
                $tree = new IntLiteral(Integer.parseInt($in.text));
            } catch (NumberFormatException e) {
		throw new IntegerTooBig(this, $ctx);
            }
        }
    | fd=FLOAT {
            try {
                $tree = new FloatLiteral(Float.parseFloat($fd.text));
                if (Float.parseFloat($fd.text) == 0 && !$fd.text.equals("0.0")){
                    throw new Exception();
                }
            } catch (Exception e) {
		            throw new FloatTooBig(this, $ctx);
            }
        }
    | st=STRING {
    	$tree = new StringLiteral($st.text);
        }
    | TRUE {
        $tree = new BooleanLiteral(true);
        }
    | FALSE {
        $tree = new BooleanLiteral(false);
        }
    | THIS {
        $tree = new This();
        setLocation($tree, $THIS);
        }
    | NULL {
        $tree = new Null();
        setLocation($tree, $NULL);
        }
    ;

ident returns[AbstractIdentifier tree]
    : e=IDENT {
    		$tree = new Identifier(tableSymbol.create($e.getText()));
        }
    ;

/****     Class related rules     ****/

list_classes returns[ListDeclClass tree]
@init   {
            $tree = new ListDeclClass();
        }
    :
      (c1=class_decl {
            $tree.add($c1.tree);
        }
      )*
    ;

class_decl returns [DeclClass tree]
    : CLASS name=ident superclass=class_extension OBRACE class_body CBRACE {
            assert($name.tree != null);
            assert($superclass.tree != null);
            assert($class_body.decls_fields != null);
            assert($class_body.decls_methods != null);
            $tree = new DeclClass($name.tree, $superclass.tree, $class_body.decls_fields, $class_body.decls_methods);
            setLocation($tree, $CLASS);
            setLocation($name.tree, $name.start);
            setLocation($superclass.tree, $superclass.start);
        }
    ;

class_extension returns[AbstractIdentifier tree]
    : EXTENDS ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($tree, $ident.start);
        }
    | /* epsilon */ {
            // A FAIRE : Gestion pour object ? (cf env_type_predef p9 poly papier)
    		$tree = new Identifier(tableSymbol.create("Object"));
        }
    ;

class_body returns[ListDeclField decls_fields, ListDeclMethod decls_methods]
    @init {
       $decls_fields = new ListDeclField();
       $decls_methods = new ListDeclMethod();
    }
    : (m=decl_method {
            assert($m.tree != null);
            setLocation($m.tree, $m.start);
            $decls_methods.add($m.tree);
        }
      | decl_field_set[$decls_fields]{
            assert($decls_fields != null);
        }
      )*
    ;

decl_field_set[ListDeclField tree]
    //@init {
   //    $tree = new ListDeclField();
   // }
    : v=visibility t=type list_decl_field[$tree, $t.tree, $v.v]
      SEMI
    ;

visibility returns[Visibility v]
    : /* epsilon */ {
            $v = Visibility.PUBLIC;
        }
    | PROTECTED {
            $v = Visibility.PROTECTED;
        }
    ;

list_decl_field[ListDeclField l, AbstractIdentifier t, Visibility v]
    : dv1=decl_field[$t, $v]{
            assert($dv1.tree != null);
            $l.add($dv1.tree);
            setLocation($dv1.tree, $dv1.start);
        } (COMMA dv2=decl_field[$t, $v]{
            assert($dv2.tree != null);
            $l.add($dv2.tree);
            setLocation($dv2.tree, $dv2.start);
        }
      )*
    ;

decl_field[AbstractIdentifier t, Visibility v] returns[AbstractDeclField tree]
    @init {
        Initialization init_var;
    }
    : i=ident {
            assert($t != null);
            assert($i.tree != null);
            assert($v != null);
            $tree = new DeclField($v, $t, $i.tree, new NoInitialization());
            setLocation($i.tree, $i.start);
        }
      (EQUALS e=expr {
            assert($t != null);
            assert($i.tree != null);
            assert($e.tree != null);
            assert($v != null);
            init_var = new Initialization($e.tree);
            setLocation(init_var, $EQUALS);
            $tree = new DeclField($v, $t, $i.tree, init_var);
            setLocation($e.tree, $e.start);
        }
      )? {
        }
    ;

decl_method returns[DeclMethod tree]
@init {
        MethodBody method_body;
        MethodAsmBody method_asm_body;
        StringLiteral asm_body_text;
}
    : type ident OPARENT params=list_params CPARENT (block {
            // A FAIRE: Remplacer $ block par quelque chose en MethodBody ?
            assert($type.tree != null);
            assert($ident.tree != null);
            assert($params.tree != null);
            assert($block.decls != null);
            assert($block.insts != null);
            method_body = new MethodBody($block.decls, $block.insts);
            $tree = new DeclMethod($type.tree, $ident.tree, $params.tree, method_body);
            setLocation(method_body, $block.start);
            setLocation($tree, $type.start);
            setLocation($type.tree, $type.start);
            setLocation($ident.tree, $ident.start);
        }
      | ASM OPARENT code=multi_line_string CPARENT SEMI {

            // A FAIRE: La gestion ci dessous semble assez incorrecte

            assert($code.location != null);
            assert($code.text != null);
            asm_body_text = new StringLiteral($code.text);
            method_asm_body = new MethodAsmBody(asm_body_text);
            $tree = new DeclMethod($type.tree, $ident.tree, $params.tree, method_asm_body);
            setLocation($tree, $ASM);
            setLocation(asm_body_text, $code.start);
            setLocation(method_asm_body, $OPARENT);
            setLocation($type.tree, $type.start);
            setLocation($ident.tree, $ident.start);
        }
      ) {
        }
    ;

list_params returns[ListDeclParam tree]
@init {
    $tree = new ListDeclParam();
}
    : (p1=param {
            assert($p1.tree != null);
            $tree.add($p1.tree);
            setLocation($p1.tree, $p1.start);
        } (COMMA p2=param {
            assert($p2.tree != null);
            $tree.add($p2.tree);
            setLocation($p2.tree, $p2.start);
        }
      )*)?
    ;

multi_line_string returns[String text, Location location]
    : s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    | s=MULTI_LINE_STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

param returns[DeclParam tree]
    : type ident {
            assert($type.tree != null);
            assert($ident.tree != null);
            $tree = new DeclParam($type.tree, $ident.tree);
            setLocation($tree, $type.start);
            setLocation($type.tree, $type.start);
            setLocation($ident.tree, $ident.start);
        }
    ;
